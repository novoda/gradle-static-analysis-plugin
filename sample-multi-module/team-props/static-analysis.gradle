apply plugin: com.novoda.staticanalysis.StaticAnalysisPlugin
apply plugin: 'io.gitlab.arturbosch.detekt'
apply plugin: "org.jlleitschuh.gradle.ktlint"

staticAnalysis {

    penalty none

    checkstyle {
        toolVersion '8.8'
        exclude project.fileTree('src/test/java')
        configFile rootProject.file('team-props/checkstyle-modules.xml')
        includeVariants { variant -> variant.name.contains('debug') }
    }

    pmd {
        toolVersion '6.1.0'
        exclude project.fileTree('src/test/java')
        ruleSetFiles = rootProject.files('team-props/pmd-rules.xml')
        ruleSets = [] // https://stackoverflow.com/questions/32247190/pmd-exclude-pattern-with-gradle
        includeVariants { variant -> variant.name.contains('debug') }
    }

    findbugs {
        toolVersion '3.0.1'
        excludeFilter rootProject.file('team-props/findbugs-excludes.xml')
        includeVariants { variant -> variant.name.contains('debug') }
    }

    lintOptions {
        lintConfig rootProject.file('team-props/lint-config.xml')
        checkReleaseBuilds false
        warningsAsErrors true
    }

    detekt {
        profile('main') {
            input = project.file("src/main/java")
            config = rootProject.file('team-props/detekt-config.yml')
            filters = '.*test.*,.*/resources/.*,.*/tmp/.*'
            output = project.file("build/reports/detekt")
        }
    }

    ktlint {
        includeVariants { variant -> variant.name.contains('debug') }
    }
}
