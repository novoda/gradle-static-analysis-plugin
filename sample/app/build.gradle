plugins {
    id 'io.gitlab.arturbosch.detekt'
    id 'org.jlleitschuh.gradle.ktlint'
    id 'com.github.spotbugs'
}

apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'com.novoda.static-analysis'

android {
    compileSdkVersion 27

    defaultConfig {
        applicationId 'com.novoda.staticanalysis.sample'
        minSdkVersion 16
        targetSdkVersion 27
        versionCode 1
        versionName '1.0'
    }
}

dependencies {
    implementation 'com.android.support:appcompat-v7:27.1.1'
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"
    testImplementation 'junit:junit:4.12'
}

staticAnalysis {

    penalty none

    checkstyle {
        toolVersion '8.8'
        exclude project.fileTree('src/test/java')
        configFile rootProject.file('team-props/checkstyle-modules.xml')
        includeVariants { variant -> variant.name.contains('debug') }
    }

    pmd {
        toolVersion '6.1.0'
        exclude project.fileTree('src/test/java')
        ruleSetFiles = rootProject.files('team-props/pmd-rules.xml')
        ruleSets = [] // https://stackoverflow.com/questions/32247190/pmd-exclude-pattern-with-gradle
        includeVariants { variant -> variant.name.contains('debug') }
    }

    findbugs {
        toolVersion '3.0.1'
        excludeFilter rootProject.file('team-props/findbugs-excludes.xml')
        includeVariants { variant -> variant.name.contains('debug') }
    }

    spotbugs {
        excludeFilter rootProject.file('team-props/findbugs-excludes.xml')
        includeVariants { variant -> variant.name.contains('debug') }
    }

    lintOptions {
        lintConfig rootProject.file('team-props/lint-config.xml')
        checkReleaseBuilds false
        warningsAsErrors true
    }

    detekt {
        config = rootProject.files('team-props/detekt-config.yml')
        filters = '.*test.*,.*/resources/.*,.*/tmp/.*'
    }

    ktlint {
        android.set(true)
        reporters {
            reporter "checkstyle"
        }

        includeVariants { variant -> variant.name.contains('debug') }
    }
}
